buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url = "https://repo.spongepowered.org/repository/maven-public" }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
    }
}

apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'com.github.johnrengelman.shadow'

group = 'me.gavin'
version = '1.0-SNAPSHOT'
archivesBaseName = 'gavhax'

minecraft {
    version = project.forgeVersion
    runDir = "run"

    mappings = project.mcpVersion
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.

    clientJvmArgs += "-Dfml.coreMods.load=me.gavin.gavhax.injection.MixinLoader"
    serverJvmArgs += "-Dfml.coreMods.load=me.gavin.gavhax.injection.MixinLoader"

    clientRunArgs += "--mixin mixins.gavhax.json"
    serverRunArgs += "--mixin mixins.gavhax.json"
}

repositories {
    maven { url = "https://repo.spongepowered.org/repository/maven-public" }
}

dependencies {
    compile("org.spongepowered:mixin:0.7.4-SNAPSHOT") {
        exclude module: 'launchwrapper'
        exclude module: 'guava'
        exclude module: 'gson'
        exclude module: 'commons-io'
    }
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"

        // replace version and mcversion
        expand "version": project.version, "mcversion": project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }

    rename "(.+_at.cfg)", 'META-INF/$1'
}

shadowJar() {
    dependencies {
        include(dependency('org.spongepowered:mixin'))
    }
    exclude 'dummyThing'
    exclude 'LICENSE.txt'
    classifier = 'release'
}

reobf {
    shadowJar {
        mappingType = 'SEARGE'
        classpath = sourceSets.main.compileClasspath
    }
}

mixin {
    defaultObfuscationEnv searge
    add sourceSets.main, 'mixins.gavhax.refmap.json'
}

jar {
    manifest {
        attributes(
                "FMLAT": "gavhax_at.cfg",
                "FMLCorePlugin": "me.gavin.gavhax.injection.MixinLoader",
                "FMLCorePluginContainsFMLMod": "true",
                "ForceLoadAsMod": "true",
                "TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
                "TweakOrder": "0",
                "MixinConfigs": "mixins.gavhax.json"
        )
    }
}

build.dependsOn(shadowJar)